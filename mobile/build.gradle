apply plugin: 'com.android.application'
apply plugin: 'crashlytics'

def tmdbApiKey = hasProperty('tmdbApiKey') ? tmdbApiKey : 'provide this in gradle.properties'
def crashlyticsApiKey = project.hasProperty('crashlyticsApiKey') ? crashlyticsApiKey : System.env.crashlyticsApiKey
def crashlyticsApiSecret = project.hasProperty('crashlyticsApiSecret') ? crashlyticsApiSecret : System.env.crashlyticsApiSecret
def signingStoreFile = hasProperty('signingStoreFile') ? signingStoreFile : 'provide this in gradle.properties'
def signingStorePassword = hasProperty('signingStorePassword') ? signingStorePassword : 'provide this in gradle.properties'
def signingKeyAlias = hasProperty('signingKeyAlias') ? signingKeyAlias : 'provide this in gradle.properties'
def signingKeyPassword = hasProperty('signingKeyPassword') ? signingKeyPassword : 'provide this in gradle.properties'

dependencies {
    compile project(":core")

    compile 'com.android.support:recyclerview-v7:22.1.1'
    compile "com.android.support:appcompat-v7:22.1.1"
    compile 'com.android.support:support-v4:22.1.1'

    compile 'com.github.bumptech.glide:glide:3.5.2'
    compile 'com.jpardogo.materialtabstrip:library:1.0.9'
    compile 'de.hdodenhof:circleimageview:1.2.2'
    compile 'com.ataulm:rv-tools:0.0.3'
    compile 'com.ataulm:view-pager-adapter:0.0.1'

    compile 'com.crashlytics.android:crashlytics:1.1.13'
}

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        applicationId "com.ataulm.wutson2"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 13
        versionName '_dog-food'

        Calendar instance = Calendar.getInstance()
        instance.setTimeInMillis(System.currentTimeMillis())
        def buildTime = instance.getTime()
        buildConfigField "String", "BUILD_TIME", "\"$buildTime\""
        buildConfigField "String", "TMDB_API_KEY", "\"$tmdbApiKey\""
    }

    signingConfigs {
        release {
            storeFile file(signingStoreFile)
            storePassword signingStorePassword
            keyAlias signingKeyAlias
            keyPassword signingKeyPassword
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        lintConfig file("lint-config.xml")
    }

    File crashlyticsProperties = new File("${project.projectDir.absolutePath}/crashlytics.properties")
    applicationVariants.all { variant ->
        def variantName = variant.name.capitalize()
        def crashlyticsGenerateResourcesTask = project.tasks.getByName("crashlyticsGenerateResources${variantName}")
        def myGenerateCrashlyticsPropertiesFileTask = task("crashlyticsGenerateProperties${variantName}") << {
            Properties properties = new Properties()
            println "...copying apiKey for $variantName"
            properties.put("apiKey", crashlyticsApiKey)
            println "...copying apiSecret for $variantName"
            properties.put("apiSecret", crashlyticsApiSecret)
            properties.store(new FileWriter(crashlyticsProperties), "")
        }
        crashlyticsGenerateResourcesTask.dependsOn myGenerateCrashlyticsPropertiesFileTask
        crashlyticsGenerateResourcesTask.doLast {
            println "...removing crashlytics.properties"
            crashlyticsProperties.delete()
        }
    }

}
